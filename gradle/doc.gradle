
apply plugin: 'io.github.swagger2markup'
apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.asciidoctor.convert'


def asciiDocInputDir = file('src/docs/developer-guide')
def asciiDocOutputDir = file('build/docs/developer-guide')


convertSwagger2markup {
    swaggerInput file("src/main/resources/static/swagger.json").getAbsolutePath()
    outputDir asciiDocOutputDir
    config = ['swagger2markup.markupLanguage' : 'ASCIIDOC',
              'swagger2markup.pathsGroupedBy' : 'TAGS',
              'swagger2markup.extensions.dynamicDefinitions.contentPath' : file(new File(asciiDocInputDir,'extensions/definitions')).absolutePath,
              'swagger2markup.extensions.dynamicOverview.contentPath' : file(new File(asciiDocInputDir,'extensions/overview')).absolutePath,
              'swagger2markup.extensions.dynamicPaths.contentPath' : file(new File(asciiDocInputDir,'extensions/paths')).absolutePath,
              'swagger2markup.extensions.dynamicSecurity.contentPath' : file(new File(asciiDocInputDir,'extensions/security')).absolutePath]
}


dependencies {
    // add converters and extensions using `asciidoctor` configuration
    asciidoctor 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.15'
    gems 'rubygems:asciidoctor-diagram:1.5.4'
}


asciidoctor {
    dependsOn convertSwagger2markup
    dependsOn jrubyPrepare
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepare.outputDir
    sourceDir asciiDocInputDir
    outputDir asciiDocOutputDir
    sources {
        include 'index.adoc'
    }
    backends = ['html5','pdf']
    attributes = [
            doctype: 'book',
            toc: 'left',
            toclevels: '3',
            'imagesdir': 'images',
            numbered: '',
            sectlinks: '',
            sectanchors: '',
            hardbreaks: '',
            generated: asciiDocOutputDir
    ]
}

// ./gradlew convertSwagger2markup
// ./gradlew asciidoctor

check.dependsOn asciidoctor